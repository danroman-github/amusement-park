// @strict-types


#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает цену номенклатуры, действующую на указанную дату
// 
// Параметры:
//  Номенклатура - СправочникСсылка.Номенклатура - Номенклатура, для которой необходимо получить цену
//  Дата - Дата, Неопределено - Дата
// 
// Возвращаемое значение:
//  Число - Цена номенклатуры
Функция ЦенаНоменклатуры(Номенклатура, Дата = Неопределено) Экспорт

	Выборка = ВыборкаЦеныНоменклатурыСрезПоследних(Номенклатура, Дата);
	
	Пока Выборка.Следующий() Цикл
		Возврат Выборка.Цена;
	КонецЦикла;
	
	Возврат 0;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Выборка цены номенклатуры срез последних.
// 
// Параметры:
//  Номенклатура - СправочникСсылка.Номенклатура - Номенклатура
//  Дата - Дата, Неопределено - Дата
// 
// Возвращаемое значение:
//  ВыборкаИзРезультатаЗапроса - Выборка цены номенклатуры срез последних:
//  *Цена - Число
//
Функция ВыборкаЦеныНоменклатурыСрезПоследних(Номенклатура, Дата)

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЦеныНоменклатурыСрезПоследних.Цена
		|ИЗ
		|	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&Период, Номенклатура = &Номенклатура) КАК
		|		ЦеныНоменклатурыСрезПоследних";
	
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("Период", Дата);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат Выборка;
	
КонецФункции

#КонецОбласти

#КонецЕсли
